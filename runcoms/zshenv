# Define environment variables.

# Am I on a mac?
[[ $OSTYPE == darwin* ]] && on_mac=1 || on_mac=0
# Am I on an SSH connection?
[[ -n $SSH_CONNECTION ]] && on_ssh=1 || on_ssh=0

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    zcompile "$zcompdump"
  fi
} &!

#
# Browser
#

if [[ $on_mac = 1 ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if [[ $on_ssh = 1 ]]; then
  export EDITOR='vim'
  export VISUAL='vim'
  export EDITOR_ASYNC=$EDITOR
else
  export EDITOR='subl -w'
  export VISUAL='subl -w'
  # Want to be able to edit a file in the background.
  export EDITOR_ASYNC='subl'
fi
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  ~/.local/bin
  /usr/local/{bin,sbin}
  $path
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

function set_proxy () {
    # Apparently some programs inspect the uppercase versions.
    # Source: https://wiki.archlinux.org/index.php/proxy_settings
    export http_proxy="http://$1"
    export HTTP_PROXY=$http_proxy
    export https_proxy="http://$1"
    export HTTPS_PROXY=$https_proxy
}
function unset_proxy () {
    unset http_proxy HTTP_PROXY
    unset https_proxy HTTPS_PROXY
}

if [[ -s "${ZDOTDIR:-$HOME}/.zshenv.local" ]]; then
  source "${ZDOTDIR:-$HOME}/.zshenv.local"
fi
